apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.williamcomartin.plexpyremote"
        minSdkVersion 17
        targetSdkVersion 25
        if (System.getenv("TRAVIS")) {
            versionCode System.getenv("TRAVIS_BUILD_NUMBER").toInteger()
            versionName "1.4"
        }

        manifestPlaceholders = [onesignal_app_id: "3b4b666a-d557-4b92-acdf-e2c8c4b95357",
                                onesignal_google_project_number: "159358117861"]
    }


    signingConfigs {
        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv("KEYSTORE_PASS")
            keyPassword System.getenv("ALIAS_PASS")
            keyAlias "PlexPyRemoteProdKey"
        }
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword "android"
            keyAlias "PlexPyRemote"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

if (System.getenv("TRAVIS")) {
    play {
        serviceAccountEmail = System.getenv("SERVICE_EMAIL")
        pk12File = rootProject.file('play.p12')
        track = 'alpha' // or 'alpha' or 'beta' or 'production'
        uploadImages = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.android.support:percent:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'

    compile 'com.google.firebase:firebase-core:10.2.1'

    // HTTP Request
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'com.google.code.gson:gson:2.5'

    compile 'org.jdeferred:jdeferred-android-aar:1.2.4'

    // Icons
    compile 'com.joanzapata.iconify:android-iconify:2.2.2'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.2'
    compile 'com.joanzapata.iconify:android-iconify-material:2.2.2'

    // QR Scanner
    compile 'com.journeyapps:zxing-android-embedded:3.0.2@aar'
    compile 'com.google.zxing:core:3.2.0'

    // 3rd Party UI
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.11'

    compile 'com.onesignal:OneSignal:3.+@aar'
    // Required for OneSignal, even if you have added FCM.
    compile 'com.google.android.gms:play-services-gcm:10.2.1'
    // Required for geotagging
    compile "com.google.android.gms:play-services-location:10.2.1"

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
